namespace Percupuff
{
    enum OscillatorShape
    {
        sine,
        triangle
    }

    processor Oscillator(OscillatorShape shape = OscillatorShape::sine) {
        input event float frequencyIn;
        input stream float frequencyModIn;
        output stream float out;

        const int tableSize = 4096;
        const float phaseToIndexMultiplier = float(tableSize) / PI_PI;

        float[tableSize] sineTable;

        float phase = 0.0f;
        float phaseIncrement = 0.0f;

        event frequencyIn(float frequency) {
            phaseIncrement = (PI_PI * frequency) / float(processor.frequency);
        }

        void init() {
            if (shape == OscillatorShape::triangle) {
                for (wrap<tableSize> i) {
                    float t = float(i) / float(tableSize);
                    if (t < 0.5f) {
                        sineTable[i] = 4.0f * t - 1.0f;
                    } else {
                        sineTable[i] = 3.0f - 4.0f * t;
                    }
                }
            } else {
                for (wrap<tableSize> i) {
                    sineTable[i] = sin(2.0f * PI * float(i) / float(tableSize));
                }
            }
        }

        void main() {
            let referenceSampleRate = 44100.0f;
            let fmAdjust = (referenceSampleRate / float(processor.frequency));
            loop {
                phase += phaseIncrement + frequencyModIn * fmAdjust;
                
                if (phase < 0) {
                    phase += PI_PI;
                }
                if (phase >= PI_PI) {
                    phase -= PI_PI;
                }

                let indexFloat = phase * phaseToIndexMultiplier;
                let index = int(indexFloat);
                let nextIndex = index + 1;
                let frac = indexFloat - index;

                let currentSample = sineTable[wrap<tableSize>(index)];
                let nextSample = sineTable[wrap<tableSize>(nextIndex)];
                let sample = currentSample * (1.0f - frac) + nextSample * frac;
                out <- sample;
                advance();
            }
        }
    }
}
