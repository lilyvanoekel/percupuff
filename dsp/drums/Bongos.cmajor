namespace Percupuff
{
    namespace Drums
    {
        processor Bongos {
            input event std::notes::NoteOn eventIn;
            output stream float<2> out;
            input event Params paramsIn;

            const float64 fs = processor.frequency;
            float64 energyPerSample = 44100.0 / fs;
            float64 b0, b1, a1, a2;

            float64 y1 = 0.0f, y2 = 0.0f;

            float64 triggerVelocity = 0.0f;
            float64 velocity = 0.0f;
            float64 gain = 0.0f;
            float panning = 0.0f;

            int highBongoPitch = 0;
            int lowBongoPitch = 0;
            float outputLevel = 0.5f;

            event paramsIn(Params p) {
                highBongoPitch = int(p.bongo1Midi);
                lowBongoPitch = int(p.bongo2Midi);
                outputLevel = p.bongoLevel * 0.01f;
                panning = p.bongoPanning;
            }

            std::random::RNG rng;
            
            event eventIn(std::notes::NoteOn n) {
                if (int(n.pitch) == highBongoPitch) {
                    triggerVelocity = n.velocity;
                    envelope.attackIn <- 0.001f;
                    envelope.releaseIn <- .2f;
                    setFrequency(650.0f, 50.0f);
                    y1 = 0.0f;
                    y2 = 0.0f;
                    rng.seed(1999);
                }
                if (int(n.pitch) == lowBongoPitch) {
                    triggerVelocity = n.velocity;
                    envelope.attackIn <- 0.001f;
                    envelope.releaseIn <- .2f;
                    setFrequency(480.0f, 50.0f);
                    y1 = 0.0f;
                    y2 = 0.0f;
                    rng.seed(1999);
                }
            }

            void init() {
                setFrequency(2300.0f, 50.0f);
            }

            void setFrequency(float64 f, float64 q) {
                float64 K = tan(pi * f / fs);
                float64 norm = 1.0 / (1.0 + K / q + K * K);
                
                b0 = K / q * norm;
                b1 = 0.0;
                a1 = 2.0 * (K * K - 1.0) * norm;
                a2 = (1.0 - K / q + K * K) * norm;

                float64 magnitude = abs(b0 + b1 + a1 + a2);
                b0 /= magnitude;
                b1 /= magnitude;
            }

            node envelope = Envelope;

            void main()
            {
                loop
                {
                    float64 sample = 0.0f;

                    if (triggerVelocity > 0.0f)
                    {
                        velocity = triggerVelocity;
                        triggerVelocity = 0.0f;
                        envelope.triggerIn <- void;
                        envelope.advance();
                        gain = envelope.gainOut;
                    }

                    if (gain > 0.0001f)
                    {
                        float64 xn = rng.getBipolar() * sqrt(energyPerSample);
                        float64 yn = b0 * xn + b1 * y1 - a1 * y1 - a2 * y2;

                        y2 = y1;
                        y1 = yn;

                        sample = yn * velocity * gain * 3.0f * outputLevel;
                        envelope.advance();
                        gain = envelope.gainOut;
                    }

                    float pan = panning * 0.01f;
                    float leftGain  = 0.5f * (1.0f - pan);
                    float rightGain = 0.5f * (1.0f + pan);
                    out <- (float(sample) * leftGain, float(sample) * rightGain);

                    advance();
                }
            }
        }
    }
}