namespace Percupuff
{
    namespace Drums
    {
        processor SideStick {
            input event (std::notes::NoteOn) eventIn;
            input stream float noiseIn;
            output stream float<2> out;
            input event Params paramsIn;
            
            float triggerVelocity = 0.0f;
            int midiNotePitch = 0;
            float outputLevel = 0.5f;
            float panning = 0.0f;

            float velocitySensitivity = 1.0f;

            event paramsIn(Params p) {
                midiNotePitch = int(p.sideStickMidi);
                outputLevel = p.sideStickLevel * 0.01f;
                panning = p.sideStickPanning;
                velocitySensitivity = p.sideStickVelocity;
            }

            event eventIn(std::notes::NoteOn n) {
                if (int (n.pitch) == midiNotePitch) {
                    triggerVelocity = sqrt(n.velocity);
                }
            }

            node envelope = Envelope;
            node osc1 = Oscillator(OscillatorShape::triangle);
            node noise = Oscillator(OscillatorShape::sine);

            node clickEnvelope = Envelope;
            const float highPassCutoff = 1400.0f;
            node highpass = std::filters::butterworth::Processor(
                std::filters::butterworth::Mode::highPass, highPassCutoff
            );            

            void main()
            {
                envelope.attackIn <- .001f;
                osc1.frequencyIn <- 387.0f;
                noise.frequencyIn <- 0.0f;

                clickEnvelope.attackIn <- .001f;

                let invSampleRate = 1.0f / float(processor.frequency);

                
                loop
                {
                    while (triggerVelocity == 0) {
                        advance();
                    }

                    let vel = triggerVelocity;
                    triggerVelocity = 0.0f;

                    envelope.releaseIn <- .2f + vel * 0.4f;
                    envelope.triggerIn <- void;
                    clickEnvelope.releaseIn <- .25f;
                    envelope.advance();
                    clickEnvelope.advance();
                    float gain = envelope.gainOut;
                    float clickGain = clickEnvelope.gainOut;
                    while (gain > 0.0f && triggerVelocity == 0.0f) {
                        // Removes frequencies below highPassCutoff.
                        osc1.frequencyModIn <- gain * 0.1f;
                        highpass.in <- osc1.out;
                        let outSample = (sin((osc1.out * gain) * (noise.out * clickGain) + highpass.out)) * 0.5f * vel * outputLevel;

                        float pan = panning * 0.01f;
                        float leftGain  = 0.5f * (1.0f - pan);
                        float rightGain = 0.5f * (1.0f + pan);
                        out <- (outSample * leftGain, outSample * rightGain);

                        osc1.advance();
                        
                        gain = envelope.gainOut;
                        envelope.advance();
                        clickEnvelope.advance();
                        noise.advance();
                        highpass.advance();
                        advance();
                    }
                }
            }
        }
    }
}
