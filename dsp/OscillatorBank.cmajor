namespace Percupuff
{
    processor OscillatorBank(OscillatorShape shape = OscillatorShape::sine) {
        input event float<8> frequencyIn;
        input event float<8> levelIn;
        input stream float frequencyModIn;
        output stream float out;

        const int tableSize = 4096;
        const float phaseToIndexMultiplier = float(tableSize) / PI_PI;

        float[tableSize] sineTable;

        float<8> phase = 0.0f;
        float<8> phaseIncrement = 0.0f;
        float<8> level = (1.0f);

        event frequencyIn(float<8> frequency) {
            phaseIncrement = (PI_PI * frequency) / float<8>(processor.frequency);
        }

        event levelIn(float<8> l) {
            level = l / sum(l);
        }

        void init() {
            if (shape == OscillatorShape::triangle) {
                for (wrap<tableSize> i) {
                    float t = float(i) / float(tableSize);
                    if (t < 0.5f) {
                        sineTable[i] = 4.0f * t - 1.0f;
                    } else {
                        sineTable[i] = 3.0f - 4.0f * t;
                    }
                }
            } else {
                for (wrap<tableSize> i) {
                    sineTable[i] = sin(2.0f * PI * float(i) / float(tableSize));
                }
            }
        }

        void main() {
            loop {
                phase += phaseIncrement + frequencyModIn;
                phase = fmod(phase, float<8>(PI_PI)); 
                phase = select(phase < 0.0f, phase + PI_PI, phase);

                let indexFloat = phase * phaseToIndexMultiplier;
                let index = int<8>(indexFloat);
                let nextIndex = index + 1;
                let frac = indexFloat - index;

                float<8> currentSamples = (
                    sineTable[wrap<tableSize>(index[0])],
                    sineTable[wrap<tableSize>(index[1])],
                    sineTable[wrap<tableSize>(index[2])],
                    sineTable[wrap<tableSize>(index[3])],
                    sineTable[wrap<tableSize>(index[4])],
                    sineTable[wrap<tableSize>(index[5])],
                    sineTable[wrap<tableSize>(index[6])],
                    sineTable[wrap<tableSize>(index[7])]
                );

                float<8> nextSamples = (
                    sineTable[wrap<tableSize>(nextIndex[0])],
                    sineTable[wrap<tableSize>(nextIndex[1])],
                    sineTable[wrap<tableSize>(nextIndex[2])],
                    sineTable[wrap<tableSize>(nextIndex[3])],
                    sineTable[wrap<tableSize>(nextIndex[4])],
                    sineTable[wrap<tableSize>(nextIndex[5])],
                    sineTable[wrap<tableSize>(nextIndex[6])],
                    sineTable[wrap<tableSize>(nextIndex[7])]
                );
                
                float<8> sample = currentSamples * (1.0f - frac) + nextSamples * frac;
                sample *= level;
                out <- sum(sample);
                advance();
            }
        }
    }

    processor PolyblepOscillatorBank {
        input event float<8> frequencyIn;
        input event float<8> levelIn;
        input stream float<8> frequencyModIn;
        output stream float out;

        float<8> phase;
        float<8> increment;
        float<8> level = float<8>(1.0f) / sum(float<8>(1.0f));

        event frequencyIn (float<8> f) {
            increment = fmod (f / float(processor.frequency), float<8>(1.0f));
        }

        event levelIn(float<8> l) {
            level = l / sum(l);
        }

        float<8> polyblep (float<8> phase, float<8> increment) {
            float<8> result;

            bool<8> condition1 = phase < increment;
            float<8> p1 = phase / increment;
            float<8> value1 = (p1 + p1) - (p1 * p1) - 1.0f;

            bool<8> condition2 = phase > (1.0f - increment);
            float<8> p2 = (phase - 1.0f) / increment;
            float<8> value2 = (p2 + p2) + (p2 * p2) + 1.0f;

            result = select(condition1, value1, float<8>(0.0f));
            result = select(condition2, value2, result);
            result *= level;

            return result;
        }

        void main() {
            let referenceSampleRate = 44100.0f;
            let fmAdjust = (referenceSampleRate / float(processor.frequency));

            loop
            {
                phase = phase + increment + frequencyModIn * fmAdjust;
                phase = fmod(phase, float<8>(1.0f));
                phase = select(phase < 0.0f, phase + 1.0f, phase);

                float<8> halfPhase = fmod(phase + 0.5f, float<8>(1.0f));
                float<8> squareWave = select(phase < 0.5f, float<8>(-1.0f), float<8>(1.0f));
                float<8> polyblep1 = polyblep(phase, increment);
                float<8> polyblep2 = polyblep(halfPhase, increment);
                out <- sum(squareWave - polyblep1 + polyblep2);

                advance();
            }
        }
    }
}
